config {
    type: "view",
    schema: "pancake",
    description: "view pancake"
}

with 
customer as (
  select
    id,
    name,
    if(array_length(phone_numbers) > 0, phone_numbers[offset(0)], null) as phone_number
  from ${ref("customers")} c
  # Filter trang Facebook TTCLiNIC
  where platform = 'facebook'
  and page_name = 'TTCLiNIC'
)
,
conv as (
  select id as conversation_id,
        inserted_at,
        if(array_length(tags) > 0, array_to_string(tags, "|"), null) all_tags,

        (
          SELECT p.tag
          FROM UNNEST(COALESCE(tags, [])) AS t
          JOIN ${ref("vw_tags")} p
            ON t = p.tag and p.type = 'service_status'
          ORDER BY p.pri
          LIMIT 1
        ) AS service_status,

        (
          SELECT p.tag
          FROM UNNEST(COALESCE(tags, [])) AS t
          JOIN ${ref("vw_tags")} p
            ON t = p.tag and p.type = 'lead_type'
          ORDER BY p.pri
          LIMIT 1
        ) AS lead_type,

        (
          SELECT p.tag
          FROM UNNEST(COALESCE(tags, [])) AS t
          JOIN ${ref("vw_tags")} p
            ON t = p.tag and p.type = 'customer_status'
          ORDER BY p.pri
          LIMIT 1
        ) AS customer_status,

        NULLIF(
          ARRAY_TO_STRING(
            ARRAY(
              SELECT tag
              FROM UNNEST(tags) AS tag
              WHERE tag IN (
                select tag 
                from ${ref("vw_tags")} 
                where type = 'service_interest'
                )
            ), '|'
          ), ''
        ) AS service_interest,

        last_sent_by.admin_name as last_sent_admin,
        "https://pancake.vn/Ttclinic.vn?c_id=" || id as link,
        page_customer.id customer_id,
        page_customer.name customer_name,
        if(array_length(recent_phone_numbers) > 0, recent_phone_numbers[offset(0)], null) as phone_number
        -- recent_phone_numbers
  from ${ref("conversations")} 
  where 1=1
  and platform = "facebook"
  and page_name = "TTCLiNIC"
  and type = "INBOX" -- Hộp thoại
)
,
message as (
  select conversation_id, string_agg(sender.name || ": " || original_message, "\n" order by inserted_at) as note
  from ${ref("messages")} 
  group by conversation_id
)

select *
from (
  select conv.conversation_id,
        conv.customer_id,
        conv.inserted_at as lead_created_at,
        conv.all_tags,
        conv.service_status,
        conv.lead_type,
        "Facebook" as lead_source,
        conv.customer_status,
        conv.service_interest,
        conv.last_sent_admin,
        conv.link,
        coalesce(conv.customer_name, customer.name) as customer_name,
        coalesce(conv.phone_number, customer.phone_number) as phone_number,
        message.note
  from conv
  left join customer on customer.id = conv.customer_id
  left join message on message.conversation_id = conv.conversation_id
)
union all (
  select coalesce(customer_name, '') || '|' || coalesce(phone_number, '') as conversation_id, 
         cast(FARM_FINGERPRINT(phone_number) as string) as customer_id,
         a.*
  from (
    select 
          datetime(ngay_input) as lead_created_at,
          cast(null as string) all_tags,
          tinh_trang_tu_van as service_status,
          lead_type,
          nguon_lead as lead_source,
          case when khach_hang_status = 'KH mới' then 'KH Mới'
                when khach_hang_status = 'KH cũ' then 'KH cũ' end as customer_status,
          nhom_dich_vu_quan_tam as service_interest,
          cskh as last_sent_admin,
          cast(null as string) as link,
          ho_va_ten_kh as customer_name,
          case when length(sdt) = 9 and left(sdt, 1) <> '0' then '0' || sdt else sdt end as phone_number,
          cast(null as string) as note
    from ${ref("ttc_external_facebook")}
  ) a
)










