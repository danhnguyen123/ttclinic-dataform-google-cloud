config {
    type: "view",
    schema: "myspa",
    description: "view Myspa"
}

with   
first_last_txn as (
      select *,
            case when date_diff(current_date, last_transaction_at, day) <= 30 then "Active T30"
                  when date_diff(current_date, last_transaction_at, day) <= 60 then "Active T60"
                  when date_diff(current_date, last_transaction_at, day) <= 90 then "Inactive"
                  else "Churn" end as status_tier 
      from (
            select distinct 
                  ma_khach_hang as customer_id,
                  first_value(ngay_gio) over(customer_order) as first_transaction_at,
                  last_value(ngay_gio) over(customer_order) as last_transaction_at,
                  first_value(ten_san_pham) over(customer_order) as first_service,
                  last_value(ten_san_pham) over(customer_order) as last_service,
            from ${ref("order")}
            window customer_order as (partition by ma_khach_hang order by ngay_gio rows between unbounded preceding and unbounded following)
            )
)
,
survey as (
      SELECT ts.ten_khach_hang, 
            ts.sdt, 
            ts.hieu_biet_qua, 
            case when ms.check is not null then ms.ma_khach_hang end as first_customer_id,
            case when ms.dau_thoi_gian is not null then True else False end as first_check
      FROM ${ref("ttc_survey")} ts
      left join ${ref("mapping_survey")} ms   
      on split(ms.dau_thoi_gian, "+")[offset(0)] = split(cast(ts.dau_thoi_gian as string), "+")[offset(0)]
      and ms.sdt = ts.sdt
)

select *
from (
      select c.ma_khach_hang as customer_id, 
            c.ho_ten as full_name,
            c.so_dien_thoai as phone_number,
            c.ngay_sinh birthday,
            c.dia_chi address,
            c.phuong_xa as ward,
            c.quan_huyen as district,
            c.tinh_thanh as province,
            flt.first_transaction_at,
            flt.last_transaction_at,
            flt.first_service,
            flt.last_service,
            coalesce(flt.status_tier, "Churn") as status_tier,
            l.hang as level,
            c.nguon_kh as customer_source,
            coalesce(s1.hieu_biet_qua, s2.hieu_biet_qua) as channel
      from ${ref("customer")} c
      left join first_last_txn flt on flt.customer_id = c.ma_khach_hang
      left join ${ref("level")} l on l.ma_khach_hang = c.ma_khach_hang
      left join (
            select *
            from survey
            where first_check is True
            and first_customer_id is not null) s1 on s1.first_customer_id = c.ma_khach_hang
      left join (
            select *
            from survey
            where first_check is False) s2 on s2.sdt = right(c.so_dien_thoai, 5)                               
)
qualify row_number() over(partition by customer_id) = 1


